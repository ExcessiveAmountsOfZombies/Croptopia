import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.blamejared.com'
    }
}

archivesBaseName = archives_base_name
buildDir = rootProject.buildDir

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.mc_ver}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    modImplementation "vazkii.patchouli:Patchouli:1.17-55-FABRIC-SNAPSHOT"

    modImplementation "io.github.prospector:modmenu:${rootProject.modmenu_version}",{
        exclude(group: "net.fabricmc.fabric-api")
    }

    shade project(":shared")
    implementation(project(":shared"))
}

minecraft {
    accessWidener = file("src/main/resources/croptopia.accesswidener")
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "mod_version": rootProject.version
    }

    from(sourceSets.main.resources.srcDirs) {
        include('**/data/croptopia/recipes/**')
        expand "dependant": "c"
    }
}
tasks.processResources.dependsOn(project(":shared").tasks.processResources)

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

shadowJar {
    archiveFileName = rootProject.archives_base_name + "-${rootProject.mc_ver}-FABRIC-dev.jar"
    configurations = [project.configurations.shade]
    relocate 'dependants/platform', 'data/c'
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    remapAccessWidener = true
    addNestedDependencies = true
    //destinationDirectory = new File("${rootProject.projectDir}/build/jars")
    archiveFileName = rootProject.archives_base_name + "-${rootProject.mc_ver}-FABRIC-${rootProject.version}.jar"
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}